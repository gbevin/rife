apply plugin: 'java'

sourceCompatibility = 1.8
version = '2.0'

repositories {
    mavenCentral()
}

configurations {
    antlr4
}

dependencies {
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    antlr4 group: 'org.antlr', name: 'antlr4', version: '4.+'
}

task antlrOutputDirCreate << { mkdir(generateGrammarSource.destination) }
task generateGrammarSource(dependsOn: antlrOutputDirCreate, type: JavaExec) {
    ext {
        destination = "$buildDir/generated-sources/"
    }

    def antlrSource = 'src/main/antlr'

    inputs.dir file(antlrSource)
    outputs.dir file(destination)

    def grammars = fileTree(antlrSource).include('**/*.g4')

    main = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4
    args = ["-o", "${destination}/com/uwyn/rife/template/antlr", grammars.files].flatten()
}

task antlrTemplateDirCreate << { mkdir(antlrTemplate.destination) }
task antlrTemplate(dependsOn: [antlrTemplateDirCreate, generateGrammarSource], type: Copy) {
    ext {
        destination = "$buildDir/filtered-sources/"
    }

    from generateGrammarSource.destination
    into antlrTemplate.destination
    filter { String line ->
        line.replaceAll("org.antlr.v4", "com.uwyn.rife.antlr4")
    }
}

compileJava {
    dependsOn antlrTemplate
    source antlrTemplate.destination
}
